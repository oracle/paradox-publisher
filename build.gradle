plugins {
    id 'groovy'
    id 'idea'
    id 'application'
    id 'codenarc'
}

repositories {
    mavenCentral()
}

// Exclude log4j 1.x from transitive dependencies
configurations {
    all*.exclude group: 'log4j', module: 'log4j'
    all*.exclude group: 'org.slf4j', module: 'slf4j-log4j12'
}

def log4j2Version = '2.12.0'
dependencies {
    compile localGroovy()
    compile "org.apache.logging.log4j:log4j-api:$log4j2Version"
    compile "org.apache.logging.log4j:log4j-core:$log4j2Version"
    compile "org.apache.logging.log4j:log4j-1.2-api:$log4j2Version"
    compile "org.apache.logging.log4j:log4j-jcl:$log4j2Version"
    compile "org.apache.logging.log4j:log4j-slf4j-impl:$log4j2Version"
    compile 'org.codehaus.groovy.modules.http-builder:http-builder:0.7.1'
    compile 'commons-cli:commons-cli:1.4'
    compile 'org.apache.commons:commons-math3:3.6.1'
}

mainClassName = 'NoClassName'
['runPublishNotify', 'runTests', 'prePublish', 'publishToCouch',
    'publishToInfinity', 'publishToJira', 'publishToHipchat', 'emailCycleResults'].each { name ->
    task "${name}StartScripts"(type: CreateStartScripts) {
        mainClassName = 'com.oracle.infy.qa.paradoxpublisher.' + name.capitalize()
        applicationName = name
        outputDir = new File('src/dist/bin')
        classpath = startScripts.classpath
        defaultJvmOpts = ['-DLog4j.configurationFile=file:conf/Log4j2.xml']
        startScripts.dependsOn "${name}StartScripts"

        // The classpath generated in the .bat script lists all the jars.
        // This shortens it using a wildcard
        // Needed to prevent "input line is too long" errors running from windows cmd shell
        doLast {
            def scriptFile = file "${outputDir}/${name}.bat"
            scriptFile.text = scriptFile.text.replaceFirst(/(set CLASSPATH=.*?\\lib\\).*/,/$1*/)
        }
    }
}

group = 'com.oracle.infy.qa.paradoxpublisher'
version = '2.1.0'

distributions {
    main {
        contents {
            from("$rootDir/src/dist/") {
                include 'bin/*'
                fileMode 0755
            }
        }
    }
}

distTar {
    compression = Compression.GZIP
    extension = 'tar.gz'
}

codenarc {
    toolVersion = '1.1'
    configFile = new File('conf/codenarc.groovy')
    maxPriority1Violations = 0
    maxPriority2Violations = 0
    maxPriority3Violations = 0
    reportFormat = project.properties.'codenarc.reportFormat' ?: 'html'
}
